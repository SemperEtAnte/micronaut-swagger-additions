import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id "com.vanniktech.maven.publish" version "0.30.0"
}
group = 'io.github.semperetante'
version = mainVersion

repositories {
    mavenCentral()
}
project(':shared-sources') {
    apply plugin: 'java'
}

jar {
    from(project.sourceSets.main.output.classesDirs)
    from project(':shared-sources').sourceSets.main.output.classesDirs
}
tasks.register('sourcesJar', Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allJava
    from project(':shared-sources').sourceSets.main.allJava
}
javadoc {
    source sourceSets.main.allJava
    source project(":shared-sources").sourceSets.main.allJava
}
java {
    sourceCompatibility = JavaVersion.toVersion("21")
    targetCompatibility = JavaVersion.toVersion("21")
}
mavenPublishing {
    coordinates(project.group as String, 'micronaut-swagger-aot', project.version as String)
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)
    signAllPublications()
    pom {
        packaging = "jar"
        name.set("micronaut-swagger-aot-plugin")
        url.set("https://github.com/SemperEtAnte/micronaut-swagger-additions")
        version = project.version
        description.set("Improved swagger doc with micronaut")

        licenses {
            license {
                name.set("The Apache License, Version 2.0")
                url.set("http://www.apache.org/licenses/LICENSE-2.0.txt")
            }
        }

        scm {
            connection.set("scm:https://github.com/SemperEtAnte/micronaut-swagger-additions.git")
            developerConnection.set("scm:git@github.com:SemperEtAnte/micronaut-swagger-additions.git")
            url.set("https://github.com/SemperEtAnte/micronaut-swagger-additions")
        }

        developers {
            developer {
                id.set("SemperAnte")
                name.set("George Troshin")
                email.set("zhorik280999@gmail.com")
            }
        }
    }
}

dependencies {
    implementation("io.micronaut:micronaut-http-client:4.3.11")
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.18.2'
    compileOnly project(":shared-sources")
    implementation 'io.micronaut.aot:micronaut-aot-api:2.6.0'
    implementation 'io.micronaut.aot:micronaut-aot-core:2.6.0'

}

test {
    useJUnitPlatform()
}